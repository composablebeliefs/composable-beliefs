defmodule CB.Belief.Graph do
  @moduledoc """
  Graph operations on the composable beliefs DAG. Pure deterministic traversal -
  no LLM reasoning, same input always produces same output.

  All functions take an index (map of id => belief) built from the
  full belief list. Build once with `index/1`, pass to all operations.
  """

  @doc "Build an id => belief lookup map."
  def index(beliefs), do: Map.new(beliefs, &{&1.id, &1})

  @doc """
  Resolve a possibly-bare id to its canonical id.

  An id that matches a belief exactly resolves to itself. A bare local id
  (`b029`) resolves to the single namespaced belief whose local part
  matches (`cb:b029`). Returns `{:error, :not_found}` when nothing matches,
  or `{:error, {:ambiguous, ids}}` when a bare id matches more than one
  namespace.

  Legacy ids resolve through the letter-swap alias
  (`CB.Belief.legacy_id_alias/1`) as a last resort: `cb:c029` finds
  `cb:b029` in a migrated graph, while a graph that really contains
  `cb:c029` matches exactly and the alias never fires.
  """
  def resolve_id(beliefs, id) do
    ids = Enum.map(beliefs, & &1.id)

    if id in ids do
      {:ok, id}
    else
      case Enum.filter(ids, &(local_id(&1) == id)) do
        [canonical] -> {:ok, canonical}
        [] -> resolve_legacy_alias(beliefs, id)
        many -> {:error, {:ambiguous, Enum.sort(many)}}
      end
    end
  end

  # Legacy ids can't be minted anymore (`b` isn't legacy-shaped), so the
  # swapped retry terminates after one hop.
  defp resolve_legacy_alias(beliefs, id) do
    case CB.Belief.legacy_id_alias(id) do
      nil -> {:error, :not_found}
      swapped -> resolve_id(beliefs, swapped)
    end
  end

  @doc """
  Fetch a belief from the index by id, falling back to the legacy
  letter-swap alias when the exact id is absent - so dep lists written
  before the b-serial id migration still resolve against a migrated
  graph. An exact match always wins.
  """
  def lookup(index, id) do
    case Map.get(index, id) do
      nil ->
        case CB.Belief.legacy_id_alias(id) do
          nil -> nil
          swapped -> Map.get(index, swapped)
        end

      belief ->
        belief
    end
  end

  # Local part of an id: everything after the namespace prefix
  # (`cb:b029` -> `b029`). A bare id is its own local part.
  defp local_id(id), do: id |> String.split(":") |> List.last()

  # Dep-list membership through the legacy alias: a dep written as
  # `cb:a386` before the id migration still counts as an edge to
  # `cb:b386`.
  defp dep_member?(deps, id) do
    id in deps or Enum.any?(deps, &(CB.Belief.legacy_id_alias(&1) == id))
  end

  @doc "Direct dependencies of a belief."
  def deps(%{deps: deps}, _index) when is_list(deps), do: deps
  def deps(_, _), do: []

  @doc "Resolve dep IDs to belief structs."
  def resolve_deps(belief, index) do
    belief
    |> deps(index)
    |> Enum.map(&lookup(index, &1))
    |> Enum.reject(&is_nil/1)
  end

  @doc """
  All beliefs that depend on the given ID (reverse lookup).
  Returns direct dependents only unless `deep: true`.
  """
  def dependents(id, beliefs, opts \\ []) do
    direct =
      Enum.filter(beliefs, fn a ->
        is_list(a.deps) and dep_member?(a.deps, id)
      end)

    if Keyword.get(opts, :deep, false) do
      deep_dependents(Enum.map(direct, & &1.id), beliefs, MapSet.new([id]))
    else
      direct
    end
  end

  defp deep_dependents([], _beliefs, _visited), do: []

  defp deep_dependents(ids, beliefs, visited) do
    new_visited = MapSet.union(visited, MapSet.new(ids))

    direct =
      Enum.filter(beliefs, fn a ->
        is_list(a.deps) and
          Enum.any?(a.deps, fn d -> d in ids or CB.Belief.legacy_id_alias(d) in ids end) and
          a.id not in visited
      end)

    next_ids =
      direct
      |> Enum.map(& &1.id)
      |> Enum.reject(&MapSet.member?(new_visited, &1))

    direct ++ deep_dependents(next_ids, beliefs, new_visited)
  end

  @doc """
  Find a dependency path from `from_id` to `to_id`.
  Returns `{:ok, [id1, id2, ...]}` or `:no_path`.
  Searches both downstream (deps) and upstream (dependents).
  """
  def path(from_id, to_id, index, beliefs) do
    case bfs_path(from_id, to_id, index, :down, beliefs) do
      {:ok, p} ->
        {:ok, p}

      :no_path ->
        case bfs_path(from_id, to_id, index, :up, beliefs) do
          {:ok, p} -> {:ok, p}
          :no_path -> :no_path
        end
    end
  end

  defp bfs_path(from, to, _index, _dir, _beliefs) when from == to, do: {:ok, [from]}

  defp bfs_path(from, to, index, dir, beliefs) do
    queue = :queue.in({from, [from]}, :queue.new())
    bfs_step(queue, to, index, dir, beliefs, MapSet.new([from]))
  end

  defp bfs_step(queue, to, index, dir, beliefs, visited) do
    case :queue.out(queue) do
      {:empty, _} ->
        :no_path

      {{:value, {current, path}}, rest} ->
        neighbors =
          case dir do
            :down ->
              case Map.get(index, current) do
                nil -> []
                a -> a.deps || []
              end

            :up ->
              beliefs
              |> Enum.filter(fn a -> is_list(a.deps) and current in a.deps end)
              |> Enum.map(& &1.id)
          end

        case Enum.find(neighbors, &(&1 == to)) do
          nil ->
            {new_queue, new_visited} =
              Enum.reduce(neighbors, {rest, visited}, fn n, {q, v} ->
                if MapSet.member?(v, n) do
                  {q, v}
                else
                  {:queue.in({n, path ++ [n]}, q), MapSet.put(v, n)}
                end
              end)

            bfs_step(new_queue, to, index, dir, beliefs, new_visited)

          _ ->
            {:ok, path ++ [to]}
        end
    end
  end

  @doc """
  Supersession history for a belief.
  Returns `{predecessors, successors}` where each is a list of beliefs
  in chronological order.
  """
  def history(id, beliefs) do
    idx = index(beliefs)
    successors = walk_successors(id, idx)
    predecessors = walk_predecessors(id, beliefs)
    {Enum.reverse(predecessors), successors}
  end

  defp walk_successors(id, index, visited \\ MapSet.new()) do
    if MapSet.member?(visited, id) do
      []
    else
      case lookup(index, id) do
        nil ->
          []

        a ->
          case a.superseded_by do
            nil ->
              []

            next_id ->
              case lookup(index, next_id) do
                nil -> []
                next -> [next | walk_successors(next_id, index, MapSet.put(visited, id))]
              end
          end
      end
    end
  end

  defp walk_predecessors(id, beliefs, visited \\ MapSet.new()) do
    if MapSet.member?(visited, id) do
      []
    else
      case Enum.find(beliefs, &(&1.superseded_by == id)) do
        nil -> []
        pred -> [pred | walk_predecessors(pred.id, beliefs, MapSet.put(visited, id))]
      end
    end
  end

  @doc """
  Find stale beliefs with optional cascade detection.
  Returns list of `{belief, stale_deps}` tuples.
  """
  def stale(beliefs, opts \\ []) do
    idx = index(beliefs)
    cascade = Keyword.get(opts, :cascade, false)

    superseded_ids =
      beliefs
      |> Enum.filter(&(&1.status in ~w(superseded retracted)))
      |> Enum.map(& &1.id)
      |> MapSet.new()

    direct_stale =
      beliefs
      |> Enum.filter(fn a ->
        CB.Belief.normalize_type(a.type) != "attestation" and a.status == "active" and
          Enum.any?(a.deps || [], &MapSet.member?(superseded_ids, &1))
      end)
      |> Enum.map(fn a ->
        bad = Enum.filter(a.deps || [], &MapSet.member?(superseded_ids, &1))
        {a, bad}
      end)

    if cascade do
      direct_ids = MapSet.new(Enum.map(direct_stale, fn {a, _} -> a.id end))
      cascade_stale(beliefs, idx, direct_stale, direct_ids, superseded_ids)
    else
      direct_stale
    end
  end

  defp cascade_stale(beliefs, idx, found, found_ids, problem_ids) do
    all_problem = MapSet.union(problem_ids, found_ids)

    next =
      beliefs
      |> Enum.filter(fn a ->
        CB.Belief.normalize_type(a.type) != "attestation" and a.status == "active" and
          not MapSet.member?(found_ids, a.id) and
          Enum.any?(a.deps || [], &MapSet.member?(found_ids, &1))
      end)
      |> Enum.map(fn a ->
        bad = Enum.filter(a.deps || [], &MapSet.member?(all_problem, &1))
        {a, bad}
      end)

    if next == [] do
      found
    else
      next_ids = MapSet.new(Enum.map(next, fn {a, _} -> a.id end))
      cascade_stale(beliefs, idx, found ++ next, MapSet.union(found_ids, next_ids), all_problem)
    end
  end

  @doc """
  Events in the graph since a date (inclusive): new nodes, supersessions,
  evidence appends, materializations, and retractions. Every event derives
  from a date the belief records already carry, so the view is a pure
  function of the graph - no git history involved.

  `since` is a `Date`. Returns a map of event lists, each sorted by date
  then id:

    - `:new` - beliefs whose `created` falls in the window
    - `:superseded` - `{old, successor}` pairs where the successor was
      created in the window (the successor's `created` dates the event)
    - `:evidence` - `{belief, entries}` pairs where `entries` are evidence
      items dated in the window and strictly after the belief's creation
      (same-day evidence on a new node is its founding, not an append)
    - `:materialized` - beliefs whose `materialized.date` falls in the window
    - `:retracted` - beliefs whose `retracted_on` falls in the window
  """
  def recent(beliefs, %Date{} = since) do
    idx = index(beliefs)
    iso = Date.to_iso8601(since)

    new_nodes =
      beliefs
      |> Enum.filter(&in_window?(&1.created, iso))
      |> sort_events(& &1.created)

    superseded =
      beliefs
      |> Enum.filter(&(&1.superseded_by != nil))
      |> Enum.map(&{&1, Map.get(idx, &1.superseded_by)})
      |> Enum.filter(fn {_old, succ} -> succ != nil and in_window?(succ.created, iso) end)
      |> sort_events(fn {_old, succ} -> succ.created end)

    evidence_appends =
      beliefs
      |> Enum.map(fn a ->
        appended =
          Enum.filter(a.evidence || [], fn e ->
            in_window?(e["date"], iso) and is_binary(a.created) and e["date"] > a.created
          end)

        {a, appended}
      end)
      |> Enum.reject(fn {_a, appended} -> appended == [] end)
      |> sort_events(fn {_a, appended} -> appended |> Enum.map(& &1["date"]) |> Enum.max() end)

    materializations =
      beliefs
      |> Enum.filter(&in_window?(&1.materialized && &1.materialized["date"], iso))
      |> sort_events(& &1.materialized["date"])

    retractions =
      beliefs
      |> Enum.filter(&in_window?(&1.retracted_on, iso))
      |> sort_events(& &1.retracted_on)

    %{
      new: new_nodes,
      superseded: superseded,
      evidence: evidence_appends,
      materialized: materializations,
      retracted: retractions
    }
  end

  # ISO-8601 date strings order lexicographically, so the window test is a
  # plain string comparison; a missing or malformed date never matches.
  defp in_window?(date, since_iso) when is_binary(date), do: date >= since_iso
  defp in_window?(_date, _since_iso), do: false

  defp sort_events(items, date_fn) do
    Enum.sort_by(items, &{date_fn.(&1), event_id(&1)})
  end

  defp event_id({belief, _}), do: belief.id
  defp event_id(belief), do: belief.id

  @doc "Find all beliefs about a given subject ref or type."
  def by_subject(beliefs, ref: ref) do
    Enum.filter(beliefs, fn a ->
      Enum.any?(a.subjects || [], fn s -> s["ref"] == ref end)
    end)
  end

  def by_subject(beliefs, type: type) do
    Enum.filter(beliefs, fn a ->
      Enum.any?(a.subjects || [], fn s -> s["type"] == type end)
    end)
  end

  @doc "Aggregate statistics across the graph."
  def stats(beliefs) do
    active = Enum.filter(beliefs, &(&1.status == "active"))

    by_type = Enum.frequencies_by(beliefs, &CB.Belief.normalize_type(&1.type))
    by_status = Enum.frequencies_by(beliefs, & &1.status)

    stale_count = length(stale(beliefs))

    unlinked =
      active
      |> Enum.count(
        &(CB.Belief.normalize_type(&1.type) == "prescription" and &1.materialized == nil)
      )

    artifact_schemes =
      beliefs
      |> Enum.filter(
        &(CB.Belief.normalize_type(&1.type) == "attestation" and &1.artifact != nil)
      )
      |> Enum.map(fn a ->
        case String.split(a.artifact, ":", parts: 2) do
          [scheme, _] -> scheme
          [single] -> single
        end
      end)
      |> Enum.frequencies()

    dep_depths =
      active
      |> Enum.filter(&(CB.Belief.normalize_type(&1.type) != "attestation"))
      |> Enum.map(&max_depth(&1.id, index(beliefs), MapSet.new()))
      |> Enum.sort()

    # Most depended-on: count how many times each ID appears in deps
    dep_counts =
      beliefs
      |> Enum.filter(&(&1.status == "active"))
      |> Enum.flat_map(&(&1.deps || []))
      |> Enum.frequencies()
      |> Enum.sort_by(fn {_id, count} -> -count end)
      |> Enum.take(5)

    %{
      total: length(beliefs),
      by_type: by_type,
      by_status: by_status,
      stale_count: stale_count,
      unlinked_prescriptions: unlinked,
      artifact_schemes: artifact_schemes,
      dep_depths: dep_depths,
      most_depended: dep_counts
    }
  end

  defp max_depth(id, index, visited) do
    if MapSet.member?(visited, id) do
      0
    else
      case Map.get(index, id) do
        nil ->
          0

        a ->
          deps = a.deps || []

          if deps == [] do
            0
          else
            new_visited = MapSet.put(visited, id)
            1 + (deps |> Enum.map(&max_depth(&1, index, new_visited)) |> Enum.max(fn -> 0 end))
          end
      end
    end
  end
end
